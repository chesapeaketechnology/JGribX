/*
 * Gradle build file for JGribX
 *
 * Target Gradle version: 7.3.3
 *
 * Reference: https://docs.gradle.org/7.3/userguide/building_java_projects.html
 *
 */
plugins {
    id "java"
    id "maven-publish"
}

group = 'mt.edu.um.cf2'
version = '0.6.0-SNAPSHOT'

apply from: "gradle/versions.gradle"

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

configurations {
    extraLibs
}

dependencies {
    extraLibs(
            [group: 'commons-cli', name: 'commons-cli', version: commonsVersion],
            [group: 'edu.ucar', name: 'jj2000', version: jj2000Version]
    )
    configurations.implementation.extendsFrom(configurations.extraLibs)

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsVersion
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    testImplementation group: 'junit', name: 'junit', version: junitVersion
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': 'mt.edu.um.cf2.jgribx.CommandLine'
    }
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

publishing {
    publications {
        customLibrary(MavenPublication) {
            from components.java
        }
    }
}

wrapper {
    gradleVersion = '7.3.3'
}
