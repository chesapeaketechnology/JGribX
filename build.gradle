/*
 * Gradle build file for JGribX
 *
 * Target Gradle version: 7.3.3
 *
 * Reference: https://docs.gradle.org/7.3/userguide/building_java_projects.html
 *
 */
plugins {
    id "java"
    id "maven-publish"
}

version = '0.6.0-SNAPSHOT'
group 'mt.edu.um.cf2'

apply from: "gradle/versions.gradle"

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

publishing {
    publications {
        customLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'mt.edu.um.cf2.jgribx'
            url = layout.buildDirectory.dir("repo")
        }
    }
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

wrapper {
    gradleVersion = '7.3.3'
}


configurations {
    extraLibs
    runtime
    ajc
    aspects
    api {
        extendsFrom(aspects)
    }
    testArtifacts.extendsFrom testRuntime
    offlinePluginDependency.extendsFrom(compileOnly)
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        // Expand all the project jars into the main jar so Raptor can find the CDI annotations
        (configurations.extraLibs).collect { it.isDirectory() ? it : zipTree(it) }
    }
    into('lib') {
        exclude 'log4j*'
        exclude 'slf4j*'
    }

    manifest {
        attributes 'Main-Class': 'mt.edu.um.cf2.jgribx.CommandLine'
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    extraLibs(
            [group: 'commons-cli', name: 'commons-cli', version: commonsVersion],
            [group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion],
            [group: 'edu.ucar', name: 'jj2000', version: jj2000Version]
    )
    configurations.implementation.extendsFrom(configurations.extraLibs)

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsVersion

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testImplementation group: 'junit', name: 'junit', version: junitVersion
}
